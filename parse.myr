use std
use "tok"
use "cpp"
use "err"
use "ctypes"
use "ast"
use "fold"

pkg qc =
	const parse : (pp : pp# -> node#[:])
;;

const mklocal = {p, name, ty
	var l
	
	l = std.mk([.ty=ty, .name=name, .sc=`Sclasslocal])
	std.slpush(&p.curfunc.locals, l)
	-> l
}

const mkglobal = {p, name, ty
	var g
	
	g = std.mk([.ty=ty, .name=name, .sc=`Sclassglobal])
	-> g
}

const mkenumsym = {p, name, ty
	var e
	
	e = std.mk([.ty=ty, .name=name, .sc=`Sclassenum])
	-> e
}


type lblstate = union
	`Lblforward (srcloc, int)
	`Lbldefined int
;;

type tagty = int
const tagstruct : tagty = 0
const tagunion : tagty = 1
const tagenum : tagty = 2

type parser = struct
	pp : pp#
	tok : tok
	lookahead: tok
	
	symtab : std.htab(byte[:], sym#)#[:]
	typetab : std.htab(byte[:], ctype)#[:]
	tagtab : std.htab(byte[:], (tagty, ctype))#[:]
	lbltab : std.htab(byte[:], lblstate)#
	breaks : int[:]
	conts : int[:]
	switchs : switchstmt#[:]
	curfunc : func#
;;

const inglobalscope = {p
	-> p.symtab.len == 1
}

const pushbreakcont = {p, blbl, clbl
	std.slpush(&p.breaks, blbl)
	std.slpush(&p.conts, clbl)
}

const popbreakcont = {p
	std.slpop(&p.breaks)
	std.slpop(&p.conts)
}

const pushscope = {p
	std.slpush(&p.symtab, std.mkht(std.strhash, std.streq))
	std.slpush(&p.typetab, std.mkht(std.strhash, std.streq))
	std.slpush(&p.tagtab, std.mkht(std.strhash, std.streq))
}

const popscope = {p
	var symtab, typetab, tagtab
	
	symtab = std.slpop(&p.symtab)
	typetab = std.slpop(&p.typetab)
	tagtab = std.slpop(&p.tagtab)
	std.htfree(symtab)
	std.htfree(typetab)
	std.htfree(tagtab)
}

const declaretag = {p, name, tag
	if std.hthas(p.tagtab[p.tagtab.len-1], name)
		-> false
	;;
	std.htput(p.tagtab[p.tagtab.len-1], name, tag)
	-> true
}

const lookuptag = {p, name
	for var i = p.tagtab.len-1; i >= 0 ; i--
		match std.htget(p.tagtab[i], name)
		| `std.Some tag:
			-> `std.Some tag
		| `std.None:
		;;
	;;
	-> `std.None
}

const lookuptype = {p, name
	for var i = p.typetab.len-1; i >= 0 ; i--
		match std.htget(p.typetab[i], name)
		| `std.Some ty:
			-> `std.Some ty
		| `std.None:
		;;
	;;
	-> `std.None
}

const declare = {p, name, sym
	if std.hthas(p.symtab[p.symtab.len-1], name)
		-> false
	;;
	std.htput(p.symtab[p.symtab.len-1], name, sym)
	-> true
}

const declaretype = {p, name, ty
	if std.hthas(p.typetab[p.typetab.len-1], name)
		-> false
	;;
	std.htput(p.typetab[p.typetab.len-1], name, ty)
	-> true
}

const lookup = {p, name
	for var i = p.symtab.len-1; i >= 0 ; i--
		match std.htget(p.symtab[i], name)
		| `std.Some sym:
			-> `std.Some sym
		| `std.None:
		;;
	;;
	-> `std.None
}

const parse = {pp
	var p : parser#
	var globals
	
	p = std.mk([
		.pp = pp,
		.symtab = [][:],
		.tagtab = [][:],
		.typetab = [][:],
		.breaks = [][:],
		.conts = [][:],
		.switchs = [][:],
	])
	pushscope(p)
	p.tok = cppnext(pp)
	p.lookahead = cppnext(pp)
	
	globals = [][:]
	while true
		match p.tok.kind
		| `Teof:
			break
		| _:
			std.slpush(&globals, parsedeclaration(p))
		;;
	;;
	-> globals
}

const next = {p
	p.tok = p.lookahead
	p.lookahead = cppnext(p.pp)
}

const expectobrace = {p
	match p.tok.kind
	| `Tobrace:
		next(p)
	| _:
		err(p.tok.loc, "expected '{'\n")
	;;
}

const expectcbrace = {p
	match p.tok.kind
	| `Tcbrace:
		next(p)
	| _:
		err(p.tok.loc, "expected '}'\n")
	;;
}

const expectoparen = {p
	match p.tok.kind
	| `Toparen:
		next(p)
	| _:
		err(p.tok.loc, "expected '('\n")
	;;
}

const expectcparen = {p
	match p.tok.kind
	| `Tcparen:
		next(p)
	| _:
		err(p.tok.loc, "expected ')'\n")
	;;
}

const expectcsqbrac = {p
	match p.tok.kind
	| `Tcsqbrac:
		next(p)
	| _:
		err(p.tok.loc, "expected ']'\n")
	;;
}

const expectcolon = {p
	match p.tok.kind
	| `Tcolon:
		next(p)
	| _:
		err(p.tok.loc, "expected ':'\n")
	;;
}

const expectsemi = {p
	match p.tok.kind
	| `Tsemi:
		next(p)
	| _:
		err(p.tok.loc, "expected ';'\n")
	;;
}

const parseif = {p
	var cond, iftrue, iffalse
	
	next(p)
	expectoparen(p)
	cond = parseexpr(p)
	expectcparen(p)
	iftrue = parsestmt(p)
	match p.tok.kind
	| `Telse:
		next(p)
		iffalse = `std.Some parsestmt(p)
	| _:
		iffalse = `std.None
	;;
	-> std.mk(`Sif [
		.cond=cond,
		.iftrue=iftrue,
		.iffalse=iffalse,
		.truelbl=nextlbl(p.curfunc),
		.falselbl=nextlbl(p.curfunc),
		.donelbl=nextlbl(p.curfunc),
	])
}

const parsewhile = {p
	var cond, stmt
	var breaklbl, contlbl
	
	next(p)
	expectoparen(p)
	cond = parseexpr(p)
	expectcparen(p)
	breaklbl = nextlbl(p.curfunc)
	contlbl = nextlbl(p.curfunc)
	pushbreakcont(p, breaklbl, contlbl)
	stmt = parsestmt(p)
	popbreakcont(p)
	
	-> std.mk(`Swhile [
		.cond=cond,
		.stmt=stmt,
		.contlbl=contlbl,
		.stmtlbl=nextlbl(p.curfunc),
		.breaklbl=breaklbl,
	])
}

const parsedowhile = {p
	var cond, stmt
	var breaklbl, contlbl
	
	next(p)
	breaklbl = nextlbl(p.curfunc)
	contlbl = nextlbl(p.curfunc)
	pushbreakcont(p, breaklbl, contlbl)
	stmt = parsestmt(p)
	popbreakcont(p)
	match p.tok.kind
	| `Twhile:
		next(p)
	| _:
		err(p.tok.loc, "expected 'while'\n")
	;;
	expectoparen(p)
	cond = parseexpr(p)
	expectcparen(p)
	expectsemi(p)
	
	-> std.mk(`Sdowhile [
		.cond=cond,
		.stmt=stmt,
		.contlbl=contlbl,
		.stmtlbl=nextlbl(p.curfunc),
		.breaklbl=breaklbl,
	])
}

const parsefor = {p
	var forstmt
	
	next(p)
	expectoparen(p)
	match p.tok.kind
	| `Tsemi:
		forstmt.init = `std.None
		next(p)
	| _:
		forstmt.init = `std.Some parseexpr(p)
		expectsemi(p)
	;;
	match p.tok.kind
	| `Tsemi:
		forstmt.cond = `std.None
		next(p)
	| _:
		forstmt.cond = `std.Some parseexpr(p)
		expectsemi(p)
	;;
	match p.tok.kind
	| `Tcparen:
		forstmt.step = `std.None
		next(p)
	| _:
		forstmt.step = `std.Some parseexpr(p)
		expectcparen(p)
	;;
	forstmt.contlbl=nextlbl(p.curfunc)
	forstmt.stmtlbl=nextlbl(p.curfunc)
	forstmt.breaklbl=nextlbl(p.curfunc)
	pushbreakcont(p, forstmt.breaklbl, forstmt.contlbl)
	forstmt.stmt = parsestmt(p)
	popbreakcont(p)
	
	-> std.mk(`Sfor forstmt)
}

const parseblock = {p
	var stmts
	
	stmts = [][:]
	match p.tok.kind
	| `Tobrace:
		next(p)
	| _:
		err(p.tok.loc, "expected '{{'\n")
	;;
	pushscope(p)
	while true
		match p.tok.kind
		| `Tcbrace:
			next(p)
			break
		| _:
			std.slpush(&stmts, parsedeclorstmt(p))
		;;
	;;
	popscope(p)
	-> stmts
}

const startoftype = {p, tok
	match tok
	| `Tsigned: -> true
	| `Tunsigned: -> true
	| `Tenum: -> true
	| `Tstruct: -> true
	| `Tunion: -> true
	| `Tchar: -> true
	// | `Tshort: -> true
	| `Tint: -> true
	| `Tlong: -> true
	// | `Tyunsigned: -> true
	// | `Tysigned: -> true
	| `Tvoid: -> true
	| `Tident n:
		match lookuptype(p, n)
		| `std.Some _:
			-> true
		| _:
			-> false
		;;
	| _:
		-> false
	;;
}


const startofdecl = {p
	-> startoftype(p, p.tok.kind)
}

const parsetagtype = {p
	var tagloc, tagname
	var tag, ty
	
	tagloc = p.tok.loc
	match p.tok.kind
	| `Tstruct:
		tag = tagstruct
	| `Tunion:
		tag = tagunion
	| `Tenum:
		tag = tagenum
	| _:
		err(p.tok.loc, "expected struct/union/enum\n")
	;;
	next(p)
	
	match p.tok.kind
	| `Tident tname:
		next(p)
		tagname = `std.Some tname
	| _:
		tagname = `std.None
	;;
	
	match (tagname, p.tok.kind)
	| (`std.Some name, `Tobrace):
		match std.htget(p.tagtab[p.tagtab.len - 1], name)
		| `std.Some (oldtag, oldty):
			if tag != oldtag
				err(tagloc, "struct/union/enum accessed with wrong tag type")
			;;
			match oldty
			| `Tystruct pbody:
				match pbody
				| &(`std.Some _):
					err(tagloc, "redefinition of tag {}", name)
				| &(`std.None):
				;;
				parsestructbody(p, pbody, tag == tagunion)
				-> `Tystruct pbody
			| `Tyenum pbody:
				match pbody
				| &(`std.Some _):
					err(tagloc, "redefinition of tag {}", name)
				| &(`std.None):
				;;
				parseenumbody(p, pbody)
				-> `Tyenum pbody
			| _:
				std.fatal("internal error\n")
			;;
		| `std.None:
			if tag == tagstruct || tag == tagunion
				var pbody = std.mk(`std.None)
				ty = `Tystruct pbody
				if !declaretag(p, name, (tag, ty))
					std.fatal("internal error\n")
				;;
				parsestructbody(p, pbody, tag == tagunion)
				-> ty
			else
				var pbody = std.mk(`std.None)
				ty = `Tyenum pbody
				if !declaretag(p, name, (tag, ty))
					std.fatal("internal error\n")
				;;
				parseenumbody(p, pbody)
				-> ty
			;;
			
		;;
	| (`std.None, `Tobrace):
		if tag == tagstruct || tag == tagunion
			var pbody = std.mk(`std.None)
			parsestructbody(p, pbody, tag == tagunion)
			-> `Tystruct pbody
		else
			var pbody = std.mk(`std.None)
			parseenumbody(p, pbody)
			-> `Tyenum pbody
		;;
	| (`std.Some name, _):
		match lookuptag(p, name)
		| `std.None:
			if tag == tagstruct || tag == tagunion
				ty = `Tystruct std.mk(`std.None)
			else
				ty = `Tyenum std.mk(`std.None)
			;;
			if !declaretag(p, name, (tag, ty))
				std.fatal("internal error\n")
			;;
			-> ty
		| `std.Some (oldtag, oldty):
			if oldtag != tag
				err(tagloc, "struct/union/enum accessed with wrong tag type")
			;;
			-> oldty
		;;
	| (`std.None, _):
		err(tagloc, "struct/union/enum with no name and not body")
	;;
}

const parsestructbody = {p, pbody, isunion
	var sc, basety
	var namety
	var s
	
	s = std.mk([
		.isunion = isunion,
		.align = 1,
		.size = 0,
		.membs = [][:],
		.offsets = [][:],
		.exports = std.mkht(std.strhash, std.streq),
	])
	expectobrace(p)
	while true
		match p.tok.kind
		| `Tcbrace:
			break
		| _:
		;;
		(sc, basety) = parsedeclspecs(p);
		while true
			namety = parsedecl(p, basety);
			addtostruct(s, namety)
			match p.tok.kind
			| `Tcomma:
				next(p)
			| _:
				break
			;;
		;;
		expectsemi(p);
	;;
	next(p)
	finalizestruct(s)
	pbody# = `std.Some s
}

const parseenumbody = {p, pbody
	var e, v, l
	
	e = std.mk([
		.membs=std.mkht(std.strhash, std.streq)
	])
	v = (0 : int64)
	match p.tok.kind
	| `Tobrace:
		next(p)
	| _:
		err(p.tok.loc, "expected '{'\n")
	;;
	v = 0
	while true
		match p.tok.kind
		| `Tcbrace:
			break
		| `Tident name:
			l = p.tok.loc
			next(p)
			if !declare(p, name, mkenumsym(p, name, `Tyenum pbody))
				err(l, "redefinition of {}", name)
			;;
			match p.tok.kind
			| `Tasn:
				next(p)
				match parseconstexpr(p)
				| `Constint c:
					v = c
				| `Constptr _:
					err(l, "enum value cannot be derived from a pointer\n")
				;;
			| _:
			;;
			std.htput(e.membs, name, v)
			v += 1	
			match p.tok.kind
			| `Tcomma:
				next(p)
			| _:
				break
			;;
		| _:
			err(p.tok.loc, "expected ident or '}'")
		;;
	;;
	next(p)
	pbody# = `std.Some e
}

const issclasstok = {kind
	match kind
	| `Ttypedef: -> true
	| `Textern: -> true
	| `Tregister: -> true
	| `Tauto: -> true
	| `Tstatic: -> true
	| _: -> false
	;;
}

const parsedeclspecs = {p
    
	const BITCHAR : int32 = 1<<0
	const BITSHORT : int32 = 1<<1
	const BITINT : int32  = 1<<2
	const BITLONG : int32 = 1<<3
	const BITLONGLONG : int32 = 1<<4
	const BITSIGNED : int32 = 1<<5
	const BITUNSIGNED : int32 = 1<<6
	const BITFLOAT : int32 = 1<<7
	const BITDOUBLE : int32 = 1<<8
	const BITENUM : int32 = 1<<9
	const BITSTRUCT : int32 = 1<<10
	const BITVOID : int32 = 1<<11
	const BITIDENT : int32 = 1<<12
	
	var bits : int32 = 0
	var sclass = `Sclassnone
	var ty = `Tyint
	var declpos = p.tok.loc

	while true
	    if issclasstok(p.tok.kind)
	    	match sclass
	    	| `Sclassnone:
	    	| _:
	    		err(p.tok.loc, "multiple storage classes in declaration specifiers\n")
	    	;;
	    	
	    	match p.tok.kind
			| `Ttypedef: sclass = `Sclasstypedef
			| `Textern: sclass = `Sclassglobal
			| `Tregister: sclass = `Sclasslocal
			| `Tauto: sclass = `Sclasslocal
			| `Tstatic: sclass = `Sclassstatic
			| _:
				std.fatal("unreachable\n")
			;;
			next(p)
			continue
	    ;;
	
		match p.tok.kind
		| `Tconst:
			next(p)
		| `Tvolatile:
			next(p)
		| `Tunsigned:
			if bits&(BITSIGNED|BITUNSIGNED) != 0
				goto baddecl
			;;
			bits |= BITUNSIGNED
			next(p)
		| `Tsigned:
			if bits&(BITSIGNED|BITUNSIGNED) != 0
				goto baddecl
			;;
			bits |= BITSIGNED
			next(p)
		| `Tident n:
			match lookuptype(p, n)
			| `std.Some t:
				ty = t
				bits |= BITIDENT
				next(p)
			| `std.None:
				/* decl name, not type, so we are done */
			;;
			break
		| `Tchar:
			if bits&BITCHAR != 0
				goto baddecl
			;;
			bits |= BITCHAR
			next(p)
		| `Tint:
			if bits&BITINT != 0
				goto baddecl
			;;
			bits |= BITINT
			next(p)
		| `Tlong:
			if bits&BITLONGLONG != 0
				goto baddecl
			;;
			if bits&BITLONG != 0
				bits &= ~BITLONG
				bits |= BITLONGLONG
			else
				bits |= BITLONG
			;;
			next(p)
		| `Tvoid:
			if bits&BITVOID != 0
				goto baddecl
			;;
			bits |= BITVOID
			next(p)
		| `Tstruct:
			if bits != 0
				goto baddecl
			;;
			ty = parsetagtype(p)
			bits |= BITSTRUCT
			break
		| `Tunion:
			if bits != 0
				goto baddecl
			;;
			ty = parsetagtype(p)
			bits |= BITSTRUCT
			break
		| `Tenum:
			if bits != 0
				goto baddecl
			;;
			ty = parsetagtype(p)
			bits |= BITENUM
			break
		| _:
			break
		;;
	;;
	
	match bits
	| 0:
		-> (sclass, `Tyint)
	| BITCHAR:
		-> (sclass, `Tychar)
	| BITINT:
		-> (sclass, `Tyint)
	| BITLONG:
		-> (sclass, `Tylong)
	| BITLONGLONG:
		-> (sclass, `Tyllong)
	| BITUNSIGNED|BITCHAR:
		-> (sclass, `Tyuchar)
	| BITUNSIGNED|BITINT:
		-> (sclass, `Tyuint)
	| BITUNSIGNED|BITLONG:
		-> (sclass, `Tyulong)
	| BITUNSIGNED|BITLONGLONG:
		-> (sclass, `Tyullong)
	| BITVOID:
		-> (sclass, `Tyvoid)
	| BITIDENT:
		-> (sclass, ty)
	| BITSTRUCT:
		-> (sclass, ty)
	| BITENUM:
		-> (sclass, ty)
	| _:
	;;
	
:baddecl
	err(declpos, "invalid declaration specifiers\n")
}

const blobfinalize = {blob
	var cmp = {a, b
		var off1, off2
		(off1, _, _) = a
		(off2, _, _) = b
		-> std.numcmp(off1, off2)
	}
	var off1, off2, ty
	std.sort(blob.vals, cmp)
	for var i = 0; i < blob.vals.len - 1; i++
		(off1, ty, _) = blob.vals[i]
		(off2, _, _) = blob.vals[i+1]
		if off1 + std.get(typesize(ty)) > off2
			-> false
		;;
	;;
	-> true
}

const parsestructinit = {p, s
	var neednext, it, loc
	var membname, memboffset, membty
	var subinit
	var blob
	
	
	blob = [
		.vals = [][:]
	]
	loc = p.tok.loc
	expectobrace(p)
	it = mkstructiter(s)
	while true
		match p.tok.kind
		| `Tcbrace:
			break
		| `Tdot:
			neednext = false
			next(p)
			match p.tok.kind
			| `Tident n:
				match getstructiter(s, n)
				| `std.None:
					err(p.tok.loc, "struct has no member {}", n)
				| `std.Some newiter:
					it = newiter
				;;
				next(p)
			| _:
				err(p.tok.loc, "expected ident")
			;;
			match p.tok.kind
			| `Tasn:
				next(p)
			| _:
				err(p.tok.loc, "expected '='")
			;;
		| _:
			neednext = true
		;;
		
		if neednext
			if !structiternext(&it)
				err(loc, "too many struct initializers")
			;;
		;;
		
		(membname, memboffset, membty) = structiterwalk(it)
		subinit = parseinit(p, membty)
		
		match subinit
		| `Initexpr sube:
			match foldexpr(sube)
			| `std.None:
				err(loc, "not a constant value")
			| `std.Some c:
				std.slpush(&blob.vals, (memboffset, membty, c))
			;;
		| `Initblob subblob:
			for (o, t, c) in subblob.vals
				std.slpush(&blob.vals, (o+memboffset, t, c))
			;;
		;;
		
		match p.tok.kind
		| `Tcomma:
			next(p)
		| _:
			break
		;;
		
	;;
	expectcbrace(p)
	if !blobfinalize(&blob)
		err(loc, "initializer has overlapping values\n")
	;;
	-> std.mk(blob)
}

const parseinit = {p, ty
	match ty
	| `Tystruct &(`std.None):
		std.fatal("internal error\n")
	| `Tystruct &(`std.Some s):
		-> `Initblob parsestructinit(p, s)
	| _:
		-> `Initexpr mkcast(ty, parseassign(p))
	;;
}

const parsedeclinit = {p, basety
	var name, ty
	(name, ty) = parsedecl(p, basety)
	
	match p.tok.kind
	| `Tasn:
		next(p)
		-> (name, ty, `std.Some parseinit(p, ty))
	| _:
		-> (name, ty, `std.None)
	;;
	
}

const parsedecl = {p, basety
	match p.tok.kind
	| `Tmul:
		next(p)
		-> parsedecl(p, `Typtr std.mk(basety))
	| _:
		-> parsedirectdecl(p, basety)
	;;
}

const parsedirectdecl = {p, basety
	match p.tok.kind
	| `Tident name:
		next(p)
		-> (`std.Some name, parsedecltail(p, basety))
	| _:
		-> (`std.None, parsedecltail(p, basety))
	;;
}

const parsedecltail = {p, basety
	var arrlen
	var cfunc, paramty, sclass, name
	
	while true
		match p.tok.kind
		| `Tosqbrac:
			next(p)
			match p.tok.kind
			| `Tcsqbrac:
				err(p.tok.loc, "unimplemented inferred arr size")
			| _:
			;;
			var exprloc = p.tok.loc
			match parseconstexpr(p)
			| `Constint v:
				arrlen = (v : uint32)
			| `Constptr _:
				err(exprloc, "array length cannot be derived from a pointer\n")
			;;
			expectcsqbrac(p)
			basety = `Tyarr std.mk([.len=arrlen, .subty=basety])
		| `Toparen:
			next(p)
			cfunc.isvararg = false
			cfunc.params = [][:]
			cfunc.rty = basety
			match p.tok.kind
			| `Tcparen:
				basety = `Tyfunc std.mk(cfunc)
				next(p)
				continue
			| _:
			;;
			while true
				var ploc = p.tok.loc
				
				(sclass, paramty) = parsedeclspecs(p)
				match sclass
				| `Sclassnone:
				| _:
					err(ploc, "parameters cannot have a storage class")
				;;
				(name, paramty) = parsedecl(p, paramty)
				match paramty
				| `Tyarr arr:
					paramty = `Typtr std.mk(arr.subty)
				| _:
				;;
				std.slpush(&cfunc.params, (name, paramty))
				match p.tok.kind
				| `Tcomma:
					next(p);
				| _:
					break
				;;
				
				match p.tok.kind
				| `Tellipsis:
					cfunc.isvararg = true
					next(p)
					break
				| _:
				;;
			;;
			expectcparen(p)
			basety = `Tyfunc std.mk(cfunc)
		| _:
			break
		;;
	;;
	-> basety
}

const parsedeclaration = {p
	var sclass, name, basety, ty, sym
	var decls, loc, paramsym, init
	
	decls.syms = [][:]
	(sclass, basety) = parsedeclspecs(p)
	while true
		loc = p.tok.loc
		(name, ty, init) = parsedeclinit(p, basety)
		match name
		| `std.Some n:
			match sclass
			| `Sclassnone:
				if inglobalscope(p)
					sclass = `Sclassglobal
				else
					sclass = `Sclasslocal
				;;
			| _:
			;;
			match sclass
			| `Sclassnone:
				std.fatal("internal error\n")
			| `Sclassglobal:
				sym = mkglobal(p, n, ty)
				if !declare(p, n, sym)
					err(loc, "redefinition of {}\n", n)
				;;
			| `Sclasslocal:
				if inglobalscope(p)
					err(loc, "cannot declare a local outside of a function")
				;;
				sym = mklocal(p, n, ty)
				if !declare(p, n, sym)
					err(loc, "redefinition of {}\n", n)
				;;
			| `Sclasstypedef:
				sym = std.mk([.name=n, .ty=ty, .sc=`Sclasstypedef])
				if !declaretype(p, n, ty)
					err(loc, "redefinition of type {}\n", n)
				;;
			| _:
				std.fatal("unimplemented sclass\n")
			;;
			std.slpush(&decls.syms, (sym, init))
		| `std.None:
		;;
		match p.tok.kind
		| `Tcomma:
			next(p)
		| `Tobrace:
			match sclass
			| `Sclassglobal:
			| _:
				err(p.tok.loc, "function outside of global scope")
			;;
			if decls.syms.len != 1
				err(p.tok.loc, "unexpected {\n")
			;;
			match name
			| `std.Some n:
				p.lbltab=std.mkht(std.slhash, std.sleq)
				p.curfunc = std.mk([
					.name=n,
					.locals=[][:],
					.params=[][:],
					.lblcnt= 0,
				])
			| `std.None:
				err(loc, "function requires a name\n")
			;;
			match ty
			| `Tyfunc fty:
				p.curfunc.functy = fty
			| _:
				err(loc, "non function type cannot have a function\n")
			;;
			pushscope(p)
			for (pname, pty) in p.curfunc.functy.params
				match pname
				| `std.Some n:
					paramsym = mklocal(p, n, pty)
					if !declare(p, n, paramsym)
						err(loc, "duplicate param name {}\n", n)
					;;
					std.slpush(&p.curfunc.params, paramsym)
				| `std.None:
				;;
			;;
			p.curfunc.body = parseblock(p)
			popscope(p)
			-> std.mk(`Nfunc p.curfunc)
		| _:
			break
		;;
	;;
	:done
	expectsemi(p)
	-> std.mk(`Ndecl decls)
}

const parsedeclorstmt = {p	
	if startofdecl(p)
		match parsedeclaration(p)
		| &(`Ndecl decls):
			-> std.mk(`Sdecl decls)
		| _:
			std.fatal("internal error\n")
		;;
	;;
	-> parsestmt(p)
}

const parsestmt = {p
	var expr
	
	match (p.tok.kind, p.lookahead.kind)
	| (`Tident _, `Tcolon):
		-> parselabeled(p)
	| _:
	;;
	
	match p.tok.kind
	| `Tsemi:
		next(p)
		-> std.mk(`Sempty)
	| `Tif:
		-> parseif(p)
	| `Tgoto:
		-> parsegoto(p)
	| `Tfor:
		-> parsefor(p)
	| `Twhile:
		-> parsewhile(p)
	| `Tswitch:
		-> parseswitch(p)
	| `Tcase:
		-> parsecase(p)
	| `Tdefault:
		-> parsedefault(p)
	| `Tdo:
		-> parsedowhile(p)
	| `Treturn:
		-> parsereturn(p)
	| `Tbreak:
		if p.breaks.len == 0
			err(p.tok.loc, "cannot break outside of a loop")
		;;
		next(p)
		-> std.mk(`Sgoto p.breaks[p.breaks.len - 1])
	| `Tcontinue:
		if p.conts.len == 0
			err(p.tok.loc, "cannot continue outside of a loop")
		;;
		next(p)
		-> std.mk(`Sgoto p.conts[p.conts.len - 1])
	| `Tobrace:
		-> std.mk(`Sblock parseblock(p))
	| _:
		expr = parseexpr(p)
		expectsemi(p)
		-> std.mk(`Sexpr expr)
	;;
}

const parseswitch = {p
	var sw
	
	match p.tok.kind
	| `Tswitch:
		next(p)
	| _:
		err(p.tok.loc, "expected 'switch'\n")
	;;
	sw = [
		.cases=[][:],
		.breaklbl=nextlbl(p.curfunc),
		.defaultlbl=`std.None
	]
	expectoparen(p)
	sw.expr = parseexpr(p)
	expectcparen(p)
	std.slpush(&p.switchs, &sw)
	std.slpush(&p.breaks, sw.breaklbl)
	sw.stmt = parsestmt(p)
	std.slpop(&p.breaks)
	std.slpop(&p.switchs)
	-> std.mk(`Sswitch sw)
}

const parsecase = {p
	var loc, sw, lbl
	
	lbl = nextlbl(p.curfunc)
	match p.tok.kind
	| `Tcase:
		if p.switchs.len == 0
			err(p.tok.loc, "case outside of a switch")
		;;
		sw = p.switchs[p.switchs.len-1]
		next(p)
	| _:
		err(p.tok.loc, "expected 'case'\n")
	;;
	
	loc = p.tok.loc
	match parseconstexpr(p)
	| `Constint v:
		for (caseval, _) in sw.cases
			if caseval == v
				err(loc, "duplicate case in switch")
			;;
		;;
		std.slpush(&sw.cases, (v, lbl))
	| `Constptr _:
		err(loc, "pointer derived constant cannot be used in case")
	;;
	expectcolon(p)
	-> std.mk(`Slabeled [.stmt=parsestmt(p), .lbl=lbl])
}

const parsedefault = {p
	var loc, sw, lbl
	
	lbl = nextlbl(p.curfunc)
	match p.tok.kind
	| `Tdefault:
		loc = p.tok.loc
		if p.switchs.len == 0
			err(p.tok.loc, "default outside of a switch")
		;;
		sw = p.switchs[p.switchs.len-1]
		next(p)
	| _:
		err(p.tok.loc, "expected 'default'\n")
	;;
	match sw.defaultlbl
	| `std.None:
		sw.defaultlbl = `std.Some lbl
	| `std.Some _:
		err(loc, "switch has more than one default label")
	;;
	expectcolon(p)
	-> std.mk(`Slabeled [.stmt=parsestmt(p), .lbl=lbl])
}

const parsegoto = {p
	var loc, name, lbl
	
	loc = p.tok.loc
	next(p)
	match p.tok.kind
	| `Tident n:
		name = n
		next(p)
	| _:
		err(p.tok.loc, "expected 'identifier'\n")
	;;
	
	match std.htget(p.lbltab, name)
	| `std.None:
		lbl = nextlbl(p.curfunc)
		std.htput(p.lbltab, name, `Lblforward (loc, lbl))
	| `std.Some `Lblforward (_, l):
		lbl = l
	| `std.Some `Lbldefined l:
		lbl = l
	;;
	-> std.mk(`Sgoto lbl)
}

const parselabeled = {p
	var loc, name, lbl
	
	match p.tok.kind
	| `Tident n:
		loc = p.tok.loc
		name = n
		next(p)
	| _:
		err(p.tok.loc, "expected 'identifier'\n")
	;;
	match p.tok.kind
	| `Tcolon:
		next(p)
	| _:
		err(p.tok.loc, "expected ':'\n")
	;;
	lbl = nextlbl(p.curfunc)
	match std.htget(p.lbltab, name)
	| `std.None:
		lbl = nextlbl(p.curfunc)
	| `std.Some `Lblforward (_, l):
		lbl = l
	| `std.Some `Lbldefined _:
		err(loc, "label '{}' redefined\n", name)
	;;
	std.htput(p.lbltab, name, `Lbldefined lbl)
	-> std.mk(`Slabeled [.lbl=lbl, .stmt=parsestmt(p)])
}

const parsereturn= {p
		var rexpr
		
		next(p)
		match p.tok.kind
		| `Tsemi:
			if !isvoid(p.curfunc.functy.rty)
				err(p.tok.loc, "void return in non void function\n")
			;;
			rexpr = `std.None
		| _:
			rexpr = `std.Some mkcast(p.curfunc.functy.rty,  parseexpr(p))
		;;
		expectsemi(p)
		-> std.mk(`Sreturn rexpr)
}

const parseexpr = {p
	-> parseassign(p)
}

const mkrelop = {op, l, r
	l = usualconv(l)
	r = usualconv(r)
	(l, r) = arithconv(l, r)
	-> std.mk(`Erelop [.op=op, .l=l, .r=r])
}

const mkbinop = {op, l, r
	var ty
	
	match (op, exprtype(l), exprtype(r))
	| (`Oplus , `Typtr _, `Typtr _):
		std.fatal("XXX unhandled adding two pointers")
	| (`Ominus , `Typtr _, `Typtr _):
		-> std.mk(`Eptrdiff [.l=l, .r=r])
	| (`Oplus ,`Typtr _, _):
		-> std.mk(`Eptrarith [.op=`Opadd, .ptr=l, .amnt=r])
	| (`Oplus , _, `Typtr _):
		-> std.mk(`Eptrarith [.op=`Opadd, .ptr=r, .amnt=l])
	| _:
	;;	
	l = usualconv(l)
	r = usualconv(r)
	(l, r) = arithconv(l, r)
	ty = exprtype(l)
	-> std.mk(`Ebinop [.op=op, .ty=ty, .l=l, .r=r])
}

const mkcast = {ty, expr
	var ety
	
	ety = exprtype(expr)
	if sametype(ty, ety)
		-> expr
	;;
	if !compatibletypes(ty, ety)
		err(exprloc(expr), "invalid cast {} {}", ty, ety)
	;;
	-> std.mk(`Ecast [.ty=ty, .expr=expr])
}

const tyrank = {ty
	match ty
	| `Tyllong:
		-> 4
	| `Tyullong:
		-> 4
	| `Tylong:
		-> 3
	| `Tyulong:
		-> 3
	| `Tyenum _:
		-> 2
	| `Tyint:
		-> 2
	| `Tyuint:
		-> 2
	| `Tychar:
		-> 1
	| `Tyuchar:
		-> 1
	| _:
		std.fatal("tyrank internal error\n")
	;;
}

const usualconv = {e
	match exprtype(e)
	| `Tyenum _:
	| `Tylong:
	| `Tyllong:
	| `Tyint:
	| `Tychar:
		-> mkcast(`Tyint, e)
	| `Tyulong:
	| `Tyullong:
	| `Tyuint:
	| `Tyuchar:
		-> mkcast(`Tyuint, e)
	| _:
		std.fatal("unimplemented usualconv\n")
	;;
	-> e
}

const arithconv = {l, r
	var lrank, rrank
	var invert
	var result
	
	lrank = tyrank(exprtype(l))
	rrank = tyrank(exprtype(r))
	
	if rrank > lrank
		invert = true
		(l, r) = (r, l)
	else
		invert = false
	;;
	
	result = (l, r)
	match exprtype(l)
	| `Tyllong:
		match exprtype(r)
		| `Tyullong:
			result = (mkcast(`Tyullong, l), r)
		| _:
			result = (l, mkcast(`Tyllong, r))
		;;
	| `Tylong:
		match exprtype(r)
		| `Tylong:
		| `Tyulong:
			result = (mkcast(`Tyulong, l), r)
		| _:
			result = (l, mkcast(`Tylong, r))
		;;
	| `Tyullong:
		result = (l, mkcast(`Tyullong, r))
	| `Tyulong:
		result = (l, mkcast(`Tyulong, r))
	| `Tyuint:
		result = (l, mkcast(`Tyuint, r))
	| `Tyenum _:
	| `Tyint:
		match exprtype(r)
		| `Tyuint:
			result = (mkcast(`Tyuint, l), r)
		| _:
			result = (l, mkcast(`Tyint, r))
		;;
	| _:
		std.fatal("unimplemented arithconv\n")
	;;
	
	if invert
		(r, l) = result
		result = (l, r)
	;;
	-> result
}

const parseconstexpr = {p
	var l
	
	l = p.tok.loc
	match foldexpr(parseexpr(p))
	| `std.Some c:
		-> c
	| `std.None:
		err(l, "expected a constant value\n")
	;;
}

const parseassign = {p
	var l, r, op
	
	l = parsecond(p)
	match p.tok.kind
	| `Tasn: op = `Oasn
	| `Taddeq: op = `Oaddasn
	| `Tsubeq: op = `Osubasn
	| _:
		-> l
	;;
	next(p)
	r = parseassign(p)
	-> std.mk(`Eassign [.ty=exprtype(l), .op=op, .l=l, .r=mkcast(exprtype(l), r)])
}

const parsecond = {p
	var cond, iftrue, iffalse
	
	cond = parselogorexpr(p)
	match p.tok.kind
	| `Tqmark:
		next(p)
		iftrue = parsecond(p)
		match p.tok.kind
		| `Tcolon:
			next(p)
		| _:
			err(p.tok.loc, "expected ':'\n")
		;;
		iffalse = parsecond(p)
		-> std.mk(`Econd [
			.ty=exprtype(iftrue),
			.cond=cond,
			.iftrue=iftrue,
			.iffalse=iffalse,
			.truelbl=nextlbl(p.curfunc),
			.falselbl=nextlbl(p.curfunc),
			.donelbl=nextlbl(p.curfunc),
		])
	| _:
		-> cond
	;;
}

const parselogorexpr = {p
	var l, r
	
	l = parselogandexpr(p)
	while true
		match p.tok.kind
		| `Tlor:
		| _:
			break
		;;
		next(p)
		r = parselogandexpr(p)
		l = std.mk(`Elogop [.op=`Olor, .l=l, .r=r])
	;;
	-> l
}

const parselogandexpr = {p
	var l, r
	
	l = parseorexpr(p)
	while true
		match p.tok.kind
		| `Tland:
		| _:
			break
		;;
		next(p)
		r = parseorexpr(p)
		l = std.mk(`Elogop [.op=`Oland, .l=l, .r=r])
	;;
	-> l
}

const parseorexpr = {p
	var l, r
	
	l = parsexorexpr(p)
	while true
		match p.tok.kind
		| `Tbor:
		| _:
			break
		;;
		next(p)
		r = parsexorexpr(p)
		l = mkbinop(`Oor, l, r)
	;;
	-> l
}

const parsexorexpr = {p
	var l, r
	
	l = parseandexpr(p)
	while true
		match p.tok.kind
		| `Tbxor:
		| _:
			break
		;;
		next(p)
		r = parseandexpr(p)
		l = mkbinop(`Oxor, l, r)
	;;
	-> l
}

const parseandexpr = {p
	var l, r
	
	l = parseeqlexpr(p)
	while true
		match p.tok.kind
		| `Tband:
		| _:
			break
		;;
		next(p)
		r = parseeqlexpr(p)
		l = mkbinop(`Oand, l, r)
	;;
	-> l
}

const parseeqlexpr = {p
	var l, r, op
	
	l = parserelexpr(p)
	while true
		match p.tok.kind
		| `Teq: op = `Oeq
		| `Tne: op = `One
		| _:
			break
		;;
		next(p)
		r = parserelexpr(p)
		l = mkrelop(op, l, r)
	;;
	-> l
}

const parserelexpr = {p
	var l, r, op
	
	l = parseaddexpr(p)
	while true
		match p.tok.kind
		| `Tgt: op = `Ogt
		| `Tlt: op = `Olt
		| `Tge: op = `Oge
		| `Tle: op = `Ole
		| _:
			break
		;;
		next(p)
		r = parseaddexpr(p)
		l = mkrelop(op, l, r)
	;;
	-> l
}

const parseaddexpr = {p
	var l, r, op
	
	l = parsemulexpr(p)
	while true
		match p.tok.kind
		| `Tplus: op = `Oplus
		| `Tminus: op = `Ominus
		| _:
			break
		;;
		next(p)
		r = parsemulexpr(p)
		l = mkbinop(op, l, r)
	;;
	-> l
}

const parsemulexpr = {p
	var l, r, op
	
	l = parsecastexpr(p)
	while true
		match p.tok.kind
		| `Tmul: op = `Omul
		| `Tdiv: op = `Odiv
		| `Tmod: op = `Omod
		| _:
			break
		;;
		next(p)
		r = parsecastexpr(p)
		l = mkbinop(op, l, r)
	;;
	-> l
}

const parsecastexpr = {p
	var ty, expr
	
	match p.tok.kind
	| `Toparen:
		if !startoftype(p, p.lookahead.kind)
			goto notcast
		;;
		expectoparen(p)
		ty = parsetypename(p)
		expectcparen(p)
		expr = parseunop(p)
		-> mkcast(ty, expr)
	| _:
	;;
	:notcast
	-> parseunop(p)
}

const mkincdec = {l, op, expr
	if !islval(expr)
		err(l, "++ and -- expect an lval\n")
	;;
	// XXX check type...
	-> std.mk(`Eincdec [.op=op, .expr=expr])
}

const parseunop = {p
	var operand
	var l
	
	l = p.tok.loc
	match p.tok.kind
	| `Tinc:
		next(p)
		operand = parseunop(p)
		-> mkincdec(l, `Opreinc, operand)
	| `Tdec:
		next(p)
		operand = parseunop(p)
		-> mkincdec(l, `Opredec, operand)
	| `Tband:
		next(p)
		operand = parseunop(p)
		if !islval(operand)
			err(l, "can only take the address of an lvalue")
		;;
		-> std.mk(`Eunop [.op=`Oaddr, .ty=`Typtr std.mk(exprtype(operand)), .operand=operand])
	| `Tminus:
		next(p)
		operand = parseunop(p)
		-> std.mk(`Eunop [.op=`Oneg, .ty=exprtype(operand), .operand=operand])
	| `Tlnot:
		next(p)
		operand = parseunop(p)
		-> std.mk(`Eunop [.op=`Olnot, .ty=exprtype(operand), .operand=operand])
	| `Tbnot:
		next(p)
		operand = parseunop(p)
		-> std.mk(`Eunop [.op=`Obnot, .ty=exprtype(operand), .operand=operand])
	| `Tmul:
		next(p)
		operand = parseunop(p)
		match exprtype(operand)
		|`Typtr &(`Tyvoid):
			err(l, "dereference of a void*")
		|  `Typtr &(ty):
			-> std.mk(`Eunop [.ty=ty, .op=`Oderef, .operand=operand])
		| _:
			err(l, "can only dereference pointer types")
		;;
		
	| `Tsizeof:
		-> parsesizeof(p)
	| _:
		-> parsepostexpr(p)
	;;
}


const parseseltail = {p, expr, selstruct
	var seloff, selty
	
	match p.tok.kind
	| `Tident name:
		match structmemb(selstruct, name)
		| `std.Some [.offset=off, .ty=ty]:
			seloff = off
			selty = ty
		| `std.None:
			err(p.tok.loc, "struct has no member {}\n", name)
		;;
		next(p)
	| _:
		err(p.tok.loc, "expected 'identifier'")
	;;
	-> std.mk(`Esel [.ty=selty, .expr=expr, .off=seloff])
}

const parsepostexpr = {p
	var expr, idx
	var selstruct
	var funclike, args
	var functy, paramty
	var l
	
	expr = parseprimaryexpr(p)
	while true
		l = p.tok.loc
		match p.tok.kind
		| `Tinc:
			next(p)
			expr = mkincdec(l, `Opostinc, expr)
		| `Tdec:
			next(p)
			expr = mkincdec(l, `Opostdec, expr)
		| `Tosqbrac:
			next(p)
			idx = parseexpr(p)
			expectcsqbrac(p)
			var idxty
			match exprtype(expr)
			|  `Tyarr arr:
				idxty = arr.subty
			|  `Typtr subty:
				idxty = subty#
			| _:
				err(l, "[] be used on pointers or arrays\n")
			;;
			match typesize(idxty)
			| `std.None:
				err(l, "[] cannot be used on an incomplete type\n")
			| _:
			;;
			expr = std.mk(`Eidx [.ty=idxty, .expr=expr, .idx=idx])
		| `Tdot:
			next(p)
			match exprtype(expr)
			|  `Tystruct &(`std.Some s):
				selstruct = s
			| _:
				err(l, "'.' can only be used on complete structs or unions\n")
			;;
			expr = parseseltail(p, expr, selstruct)
		| `Tarrow:
			next(p)
			match exprtype(expr)
			| `Typtr &(`Tystruct &(`std.Some s)):
				selstruct = s
			| _:
				err(l, "'->' can only be used on struct or union pointers\n")
			;;
			expr = parseseltail(p, expr, selstruct)
		| `Toparen:
			next(p)
			funclike = expr
			match exprtype(funclike)
			| `Tyfunc fty:
				functy = fty
			| _:
				err(l, "call of non function")
			;;
			args = [][:]
			match p.tok.kind
			| `Tcparen:
				next(p)
			| _:
				while true
					(_, paramty) = functy.params[args.len]
					expr = mkcast(paramty, parseexpr(p))
					std.slpush(&args, expr)
					match p.tok.kind
					| `Tcparen:
						next(p)
						break
					| `Tcomma:
						next(p)
					| _:
					;;
				;;
			;;
			expr = std.mk(`Ecall [
				.functy=functy,
				.func=funclike,
				.args=args
			])
		| _:
			break
		;;
	;;
	-> expr
}

const parsetypename = {p
	var sc, name, ty
	(sc, ty) = parsedeclspecs(p);
	(name, ty) = parsedecl(p, ty);
	/* XXX: disallow sclass or name? */
	-> ty;
}

const parsesizeof = {p
	var loc, ty
	
	// XXX: This may be incorrect.
	// needs more edge test cases.
	loc = p.tok.loc
	next(p)
	match p.tok.kind
	| `Toparen:
		expectoparen(p)
		if startoftype(p, p.tok.kind)
			ty = parsetypename(p)
		else
			ty = exprtype(parseunop(p))
		;;
		expectcparen(p)
	| _:
		if startoftype(p, p.tok.kind)
			ty = parsetypename(p)
		else
			ty = exprtype(parseunop(p))
		;;
	;;
	match typesize(ty)
	| `std.None:
		err(loc, "cannot do sizeof on incomplete type\n")
	| `std.Some sz:
		-> std.mk(`Esizeof (sz : int))
	;;
}

const parseprimaryexpr = {p
	var expr
	
	match p.tok.kind
	| `Tident n:
		match lookup(p, n)
		| `std.Some sym:
			next(p)
			-> std.mk(`Eident sym)
		| `std.None:
			err(p.tok.loc, "undefined symbol {}\n", n)
		;;
	| `Tintlit v:
		next(p)
		-> std.mk(`Eintlit v)
	| `Tchrlit c:
		next(p)
		-> std.mk(`Echrlit c)
	| `Tstrlit s:
		next(p)
		while true
			match p.tok.kind
			| `Tstrlit s2:
				next(p)
				s = std.strcat(s, s2)
			| _:
				break
			;;
		;;
		-> std.mk(`Estrlit s)
	| `Toparen:
		next(p)
		expr = parseexpr(p)
		expectcparen(p)
		-> expr
	| _:
		err(p.tok.loc, "expected expression\n")
	;;	
}

