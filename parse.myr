use std
use "tok"
use "err"
use "ctypes"

pkg qc =
	
	const parse : (ts : tokstream# -> node#[:])
	
	type node = union
		`Nfunc func#
	;;
	
	type func = struct
		name : byte[:]
		rettype : ctype
		args : (byte[:], ctype)[:]
		body : stmt#[:]
		locals : local#[:]
	;;
	
	type stmt = union
		`Sdecl decl
		`Sexpr expr#
		`Sreturn expr#
		`Sblock stmt#[:]
		`Sif ifstmt
		`Sfor forstmt
		`Swhile whilestmt
		`Sempty
	;;
	
	type ifstmt = struct
		cond : expr#
		iftrue : stmt#
		iffalse : std.option(stmt#)
		truelbl : int
		falselbl : int
		donelbl : int
	;;
	
	type forstmt = struct
		init : std.option(expr#)
		cond : std.option(expr#)
		step : std.option(expr#)
		stmt : stmt#
	;;
	
	type whilestmt = struct
		cond : expr#
		stmt : stmt#
		contlbl : int
		breaklbl : int
		stmtlbl : int
	;;
	
	type expr = union
		`Ebinop binop
		`Eunop unop
		`Eassign assign
		`Eintlit int64
		`Eident sym
	;;
	
	type op = union
		`Oplus
		`Ominus
		`Oaddr
		`Oderef
	;;
	
	type class = union
		`Sclasslocal
		`Sclassnone
	;;
	
	type binop = struct
		ty : ctype
		op : op
		l : expr#
		r : expr#
	;;
	
	type unop = struct
		ty : ctype
		op : op
		operand : expr#
	;;
	
	type assign = struct
		ty : ctype
		op : op
		l : expr#
		r : expr#
	;;
	
	type decl = struct
		name : byte[:]
		sym : sym
	;;
	
	type sym = union 
		`Symlocal local#
	;;
	
	type local = struct
		name : byte[:]
		ty : ctype
	;;
;;

const mklocal = {p, name, ty
	var l
	
	l = std.mk([.ty=ty, .name=name])
	std.slpush(&p.curfunc.locals, l)
	-> l
}

type parser = struct
	ts : tokstream#
	stab : std.htab(byte[:], sym)#[:]
	curfunc : func#
	lblcnt : int
;;

const pushscope = {p
	std.slpush(&p.stab, std.mkht(std.strhash, std.streq))
}

const popscope = {p
	var ht
	
	(ht, p.stab) = std.slpop(p.stab)
	std.htfree(ht)
}

const nextlbl = {p
	-> p.lblcnt++
}

const declare = {p, name, sym
	if std.hthas(p.stab[p.stab.len-1], name)
		-> false
	;;
	std.htput(p.stab[p.stab.len-1], name, sym)
	-> true
}

const lookup = {p, name
	for var i = p.stab.len-1; ; i--
		match std.htget(p.stab[i], name)
		| `std.Some sym:
			-> `std.Some sym
		| `std.None:
		;;
	;;
	-> `std.None
}

const parse = {ts
	var p : parser#
	var stab, globals
	
	stab = [][:]
	
	p = std.mk([
		.ts = ts,
		.stab = stab
	])
	
	globals = [][:]
	while true
		match peek(p)
		| (_, `Teof):
			break
		| _:
			std.slpush(&globals, parsefunc(p))
		;;
	;;
	-> globals
}

const next = {p
	-> toknext(p.ts)
}

const peek = {p
	-> tokpeek(p.ts)
}

const parsetype = {p
	match next(p)
	| (_, `Tint):
		-> `Typrim `Tyint
	| (l, _):
		err(l, "expected type\n")
	;;
}

const parsefunc = {p
	var rettype, fname, args
	var argname, argtype
	
	args = [][:]
	
	rettype = parsetype(p)
	match next(p)
	| (l, `Tident name):
		fname = name
	| (l, tok):
		err(l, "expected function name\n", tok)
	;;
	
	match next(p)
	| (_, `Toparen):
		match peek(p)
		| (_, `Tcparen):
			next(p)
			goto endparams
		| _:
			while true
				argtype = parsetype(p)
				match next(p)
				| (l, `Tident name):
					argname = name
				| (l, _):
					err(l, "expected parameter name\n")
				;;
				std.slpush(&args, (argname, argtype))
				match next(p)
				| (l, `Tcparen):
					break
				| (l, `Tcomma):
				| (l, _):
					err(l, "expected ',' or ')'\n")
				;;
			;;
		;;
	| (l, _):
		err(l, "expected function parameters")
	;;
	:endparams
	p.curfunc = std.mk( [
		.name = fname,
		.rettype = rettype,
		.args = args,
		.locals = [][:],
		.body = [][:],
	])
	p.lblcnt = 0
	p.curfunc.body = parseblock(p)
	-> std.mk(`Nfunc p.curfunc)
}

const expectoparen = {p
	match next(p)
	| (_, `Toparen):
	| (l, _):
		err(l, "expected '('\n")
	;;
}

const expectcparen = {p
	match next(p)
	| (_, `Tcparen):
	| (l, _):
		err(l, "expected ')'\n")
	;;
}

const parseif = {p
	var cond, iftrue, iffalse
	
	next(p)
	expectoparen(p)
	cond = parseexpr(p)
	expectcparen(p)
	iftrue = parsestmt(p)
	match peek(p)
	| (_, `Telse):
		next(p)
		iffalse = `std.Some parsestmt(p)
	| _:
		iffalse = `std.None
	;;
	-> std.mk(`Sif [
		.cond=cond,
		.iftrue=iftrue,
		.iffalse=iffalse,
		.truelbl=nextlbl(p),
		.falselbl=nextlbl(p),
		.donelbl=nextlbl(p),
	])
}

const parsewhile = {p
	var cond, stmt
	
	next(p)
	expectoparen(p)
	cond = parseexpr(p)
	expectcparen(p)
	stmt = parsestmt(p)
	-> std.mk(`Swhile [
		.cond=cond,
		.stmt=stmt,
		.contlbl=nextlbl(p),
		.stmtlbl=nextlbl(p),
		.breaklbl=nextlbl(p),
	])
}

const parsedowhile = {p
	next(p)
	parsestmt(p)
	match next(p)
	| (_, `Twhile):
	| (l, _):
		err(l, "expected 'while'\n")
	;;
	expectoparen(p)
	parseexpr(p)
	expectcparen(p)
	expectsemi(p)
	-> std.mk(`Sempty)
}

const parsefor = {p
	var forstmt
	
	next(p)
	expectoparen(p)
	match peek(p)
	| (_, `Tsemi):
		forstmt.init = `std.Some parseexpr(p)
	| _:
		forstmt.init = `std.None
	;;
	expectsemi(p)
	match peek(p)
	| (_, `Tsemi):
		forstmt.cond = `std.Some parseexpr(p)
	| _:
		forstmt.cond = `std.None
	;;
	expectsemi(p)
	match peek(p)
	| (_, `Tsemi):
		forstmt.step = `std.Some parseexpr(p)
	| _:
		forstmt.step = `std.None
	;;
	expectsemi(p)
	expectcparen(p)
	parseblock(p)
	-> std.mk(`Sfor forstmt)
}

const parseblock = {p
	var stmts
	
	stmts = [][:]
	match next(p)
	| (l, `Tobrace):
	| (l, _):
		err(l, "expected '{'\n")
	;;
	pushscope(p)
	while true
		match peek(p)
		| (_, `Tcbrace):
			next(p)
			break
		| _:
			std.slpush(&stmts, parsedeclorstmt(p))
		;;
	;;
	popscope(p)
	-> stmts
}

const expectsemi = {p
	match next(p)
	| (l, `Tsemi):
	| (l, _):
		err(l, "expected ';'")
	;;
}

const startofdecl = {p
	match peek(p)
	| (_, `Tstruct):
	| (_, `Tint):
	| (_, `Tvoid):
	| _:
		-> false
	;;
	-> true
}

const parsetagtype = {p
	match next(p)
	| (_, `Tstruct):
	| (l, _):
		err(l, "expected 'struct'\n")
	;;
	match peek(p)
	| (_, `Tident tagname):
		next(p)
	| (l, _):
	;;
	match peek(p)
	| (_, Tcbrace):
	| (l, _):
		err(l, "unimplemented tag\n")
	;;
	-> parsestructbody(p)
}

const parsestructbody = {p
	var sc, basety
	var namety, membs 
	
	membs = [][:]
	match next(p)
	| (l, `Tobrace):
	| (l, _):
		err(l, "expected '{'\n")
	;;
	while true
		match peek(p)
		| (_, `Tcbrace):
			break
		| _:
		;;
		(sc, basety) = parsedeclspecs(p);
		while true
			namety = parsedecl(p, basety);
			std.slpush(&membs, namety)
			match peek(p)
			| (_, `Tcomma):
				next(p)
			| _:
				break
			;;
		;;
		expectsemi(p);
	;;
	next(p)
	-> `Tyaggr std.mk(`Tystruct [.membs=membs])
}

const parsedeclspecs = {p
	match peek(p)
	| (l, `Tint):
		next(p)
		-> (`Sclasslocal, `Typrim `Tyint)
	| (l, `Tstruct):
		-> (`Sclasslocal, parsetagtype(p))
	| (l, _):
		err(l, "expected a type specifier or storage class\n")
	;;
}

const parsedecl = {p, basety
	match peek(p)
	| (_, `Tmul):
		next(p)
		-> parsedecl(p, `Tyaggr std.mk(`Typtr basety))
	| _:
		-> parsedirectdecl(p, basety)
	;;
}

const parsedirectdecl = {p, basety
	match peek(p)
	| (_, `Tident name):
		next(p)
		-> (`std.Some name, parsedecltail(p, basety))
	| _:
		-> (`std.None, parsedecltail(p, basety))
	;;
}

const parsedecltail = {p, basety
	while true
		match peek(p)
		| (_, `Tosqbrac):
			std.die("unimplemented parsedecltail\n")
		| _:
			-> basety
		;;
	;;
	std.die("unreachable\n")
}

const parsedeclaration = {p
	var sclass, name, ty, sym
	var loc
	
	match peek(p)
	| (l, _):
		loc = l
	;;
	(sclass, ty) = parsedeclspecs(p)
	(name, ty) = parsedecl(p, ty)
	match name
	| `std.Some n:
		sym = `Symlocal mklocal(p, n, ty)
		if !declare(p, n, sym)
			err(loc, "redefinition of {}\n", n)
		;;
		expectsemi(p)
		-> std.mk(`Sdecl [.name=n, .sym=sym])
	| `std.None:
		err(loc, "declaration defines no name\n")
	;;
}

const parsedeclorstmt = {p	
	if startofdecl(p)
		-> parsedeclaration(p)
	;;
	-> parsestmt(p)
}

const parsestmt = {p
	var rexpr
	
	match peek(p)
	| (_, `Tsemi):
		next(p)
		-> std.mk(`Sempty)
	| (_, `Tif):
		-> parseif(p)
	| (_, `Tfor):
		-> parsefor(p)
	| (_, `Twhile):
		-> parsewhile(p)
	| (l, `Treturn):
		next(p)
		rexpr = parseexpr(p)
		expectsemi(p)
		-> std.mk(`Sreturn rexpr)
	| (l, `Tobrace):
		-> std.mk(`Sblock parseblock(p))
	| _:
		rexpr = parseexpr(p)
		expectsemi(p)
		-> std.mk(`Sexpr rexpr)
	;;
}

const parseexpr = {p
	-> parseassign(p)
}

const parseassign = {p
	var l, r
	
	l = parseaddexpr(p)
	match peek(p)
	| (_, `Tasn):
		next(p)
		r = parseaddexpr(p)
		-> std.mk(`Eassign [.ty=`Typrim `Tyint, .l=l, .r=r])
	| _:
		-> l
	;;
}

const parseaddexpr = {p
	var l, r, op
	
	l = parseunop(p)
	while true
		match peek(p)
		| (_, `Tplus):
			op = `Oplus
		| (_, `Tminus):
			op = `Ominus
		| _:
			break
		;;
		next(p)
		r = parseunop(p)
		l = std.mk(`Ebinop [.ty=`Typrim `Tyint, .op=op, .l=l, .r=r])
	;;
	-> l
}

const parseunop = {p
	var operand
	
	match peek(p)
	| (l, `Tband):
		next(p)
		operand = parseunop(p)
		-> std.mk(`Eunop [.ty=`Typrim `Tyint, .op=`Oaddr, .operand=operand])
	| (l, `Tmul):
		next(p)
		operand = parseunop(p)
		-> std.mk(`Eunop [.ty=`Typrim `Tyint, .op=`Oderef, .operand=operand])
	| _:
		-> parseatom(p)
	;;
}

const parseatom = {p
	match next(p)
	| (l, `Tident n):
		match lookup(p, n)
		| `std.Some sym:
			-> std.mk(`Eident sym)
		| `std.None:
			err(l, "undefined symbol {}\n", n)
		;;
	| (l, `Tintlit v):
		-> std.mk(`Eintlit v)
	| (l, t):
		err(l, "expected 'integer literal, got {}'\n", t)
	;;
}

