use std
use "hideset"
use "err"

pkg qc =
	type tokstream = struct
		data        : byte[:]
		idx         : int
		prevtokline	: int
		indirective : bool
		includefsm  : union
			`Inclstart
			`Inclgotdir
			`Inclgotincl
		;;
		loc	: srcloc
	;;

	const tokinit	: (path : byte[:]	-> tokstream#)
	const tokinitf	: (fd : std.fd, path : byte[:]	-> tokstream#)
	const toknext	: (ts : tokstream#	-> tok)

	type tok = struct
		kind : tokkind 
		text : byte[:]
		loc : srcloc
		ws : bool
		hs : hideset
	;;

	type tokkind = union
		`Terror
		`Teof
		`Tplus    /* + */
		`Tminus   /* - */
		`Tmul     /* * */
		`Tdiv     /* / */
		`Tinc     /* ++ */
		`Tdec     /* -- */
		`Tmod     /* % */
		`Tasn     /* = */
		`Taddeq   /* += */
		`Tsubeq   /* -= */
		`Tmuleq   /* *= */
		`Tdiveq   /* /= */
		`Tmodeq   /* %= */
		`Tboreq   /* |= */
		`Tbxoreq  /* ^= */
		`Tbandeq  /* &= */
		`Tbsleq   /* <<= */
		`Tbsreq   /* >>= */
		
		`Tbor     /* | */
		`Tbxor    /* ^ */
		`Tband    /* & */
		`Tbsl     /* << */
		`Tbsr     /* >> */
		`Tbnot    /* ~ */
	
		`Teq      /* == */
		`Tgt      /* > */
		`Tlt      /* < */
		`Tge      /* >= */
		`Tle      /* <= */
		`Tne      /* != */
	
		`Tlor     /* || */
		`Tland    /* && */
		`Tlnot    /* ! */
	
		`Tobrace  /* { */
		`Tcbrace  /* } */
		`Toparen  /* ( */
		`Tcparen  /* ) */
		`Tosqbrac /* [ */
		`Tcsqbrac /* ] */
		
		`Thash         /* # */
		`Tdirective    /* # */
		`Tsysincl byte[:] /* <foo.h> */
		`Tenddirective /* \n */
	
		`Tswitch  /* switch */
		`Tcase    /* case */
		`Tdefault /* default */
		`Tdo      /* do */
		`Tfor     /* for */
		`Twhile   /* while */
		`Tif      /* if */
		`Telse    /* else */
		`Tgoto    /* goto */
		`Tbreak   /* break */
		`Tcontinue   /* continue */
		`Tstruct  /* struct */
		`Tunion   /* union */
		`Tenum    /* enum */
		`Textern  /* extern */
		`Tconst   /* const */
		`Treturn  /* return */
		`Tsizeof  /* sizeof */
		`Ttypedef /* typedef */
		`Tvoid	/* void */
		`Tchar	/* char */
		`Tint	/* int */
		`Tlong	/* long */
	
		`Tintlit int64
		`Tstrlit byte[:]
		`Tfltlit flt64
		`Tchrlit char
		
		`Tellipsis/* ... */
		`Tsemi   /* ; */
		`Tcolon   /* : */
		`Tdot     /* . */
		`Tcomma   /* , */
		`Tarrow   /* -> */
		`Tident byte[:]
	;;

;;

const Eof = std.Badchar

const tokinit = {path
	match std.slurp(path)
	| `std.Ok data: -> mklexer(path, data)
	| `std.Fail e:	std.fatal("could not read file {}: {}\n", path, e)
	;;
}

const tokinitf = {fd, name
	match std.fslurp(fd)
	| `std.Ok data: -> mklexer(name, data)
	| `std.Fail e:	std.fatal("could not read file {}: {}\n", fd, e)
	;;
}

const mklexer = {name, data
	-> std.mk([
		.loc=[.file=name, .line=1, .col=1],
		.data=data,
		.idx=0,
		.prevtokline=0,
		.indirective=false,
		.includefsm=`Inclstart
	])
}

const toknext = {ts
	var tok
	
	tok = tokread(ts)
	match tok.kind
	| `Thash:
		if ts.prevtokline != tok.loc.line
			tok.kind = `Tdirective
			ts.indirective = true
		;;
	| _:
	;;
	
	match ts.includefsm
	| `Inclstart:
		match tok.kind
		| `Tdirective: ts.includefsm = `Inclgotdir
		| _: ts.includefsm = `Inclstart
		;;
	| `Inclgotdir:
		match tok.kind
		| `Tident "include": ts.includefsm = `Inclgotincl
		| _:                 ts.includefsm = `Inclstart
		;;
	| `Inclgotincl:
		ts.includefsm = `Inclstart
	;;
	
	ts.prevtokline = tok.loc.line
	-> tok
}

const expectsysincl = {ts
	match ts.includefsm
	| `Inclgotincl: -> true
	| _: -> false
	;;
}

const tokread = {ts
	var c, loc, kind
	var start
	var ws
	
	ws = false
	
	:again
	ws = skipspace(ts) || ws
	start = ts.idx
	loc = ts.loc
	c = peekc(ts)
	if ts.idx == ts.data.len
		if ts.indirective
			ts.indirective = false
			kind = `Tdirective
		else
			kind = `Teof
		;;
	elif c == '\n'
		ws = true
		takec(ts)
		if !ts.indirective 
			goto again
		;;
		ts.indirective = false
		kind = `Tenddirective
	elif c == '\''
		kind = chrlit(ts)
	elif c == '<' && expectsysincl(ts)
		kind = sysincl(ts)
	elif c == '"'
		kind = strlit(ts)
	elif std.isdigit(c)
		kind = numlit(ts)
	elif isident(c)
		kind = kwident(ts)
	else
		kind = oper(ts)
	;;
	-> [
		.loc=loc,
		.text=ts.data[start:ts.idx],
		.kind = kind,
		.ws=ws,
	]
}

const skipspace = {ts
	var ignorenl, start

	ignorenl = false
	start = ts.idx
	while true
		match peekc(ts)
		| '\n':
			if ignorenl
				takec(ts)
			else
				break
			;;
		| '\\':
			ignorenl = true
			takec(ts)
		| '/':
			match npeekc(ts, 1)
			| '/':	skipto(ts, '\n')
			| '*':	skipcomment(ts)
			| _:	break
			;;
		| c:
			if std.isspace(c)
				takec(ts)
			else
				break
			;;
		;;
	;;
	
	if ts.idx == start
		-> false
	else
		-> true
	;;
}

const skipcomment = {ts
	var startln

	startln = ts.loc.line
	while true
		match takec(ts)
		| '*':
			if matchc(ts, '/')
				break
			;;
		| Eof:
			err(ts.loc, "file ended in comment starting on line {}\n", startln)
		| _:
		;;
	;;
}

const chrlit = {ts
	var c, close

	takec(ts)
	c = takec(ts)
	if c == '\\'
		c = unescape(ts)
	;;
	close = takec(ts)
	if close != '\''
		err(ts.loc, "expected closing ' in character literal, got {}\n", close)
	;;
	-> `Tchrlit c
}

const strlit = {ts
	var sb

	takec(ts)
	sb = std.mksb()
	while true
		match takec(ts)
		| Eof:
			err(ts.loc, "unexpected EOF within string literal\n")
		| '\n':
			err(ts.loc, "unexpected \\n within string literal\n")
		| '"':
			break
		| '\\':
			std.sbputc(sb, unescape(ts))
		| c:
			std.sbputc(sb, c)
		;;
	;;
	-> `Tstrlit std.sbfin(sb)
}

const sysincl = {ts
	var start

	takec(ts)
	start = ts.idx
	while true
		match takec(ts)
		| Eof:
			err(ts.loc, "unexpected EOF within include\n")
		| '\n':
			err(ts.loc, "unexpected \\n within include\n")
		| '>':
			break
		| c:
		;;
	;;
	-> `Tsysincl ts.data[start:ts.idx-1]
}

const unescape = {ts
	var c, c1, c2

	c = takec(ts)
	/* we've already seen the '\' */
	match c
	| 'n':	-> '\n'
	| 'r':	-> '\r'
	| 't':	-> '\t'
	| 'b':	-> '\b'
	| '"':	-> '\"'
	| '\'':	-> '\''
	| 'v':	-> '\v'
	| '\\':	-> '\\'
	| '0':	-> '\0'
	| 'u':	-> utfesc(ts);
	| 'x':
		c1 = takec(ts)
		if !std.isxdigit(c1)
			err(ts.loc, "expected hex digit, got {}\n", c1)
		;;
		c2 = takec(ts)
		if !std.isxdigit(c2)
			err(ts.loc, "expected hex digit, got {}\n", c2)
		;;
		-> 16*std.charval(c1, 16) + std.charval(c2, 16)

		c2 = takec(ts)
	| esc:
		err(ts.loc, "unknown escape code \\{}\n", esc)
	;;
}

const utfesc = {ts
	var c, v

	if takec(ts) != '{'
		err(ts.loc, "\\u escape sequence without initial '{'\n")
	;;
	v = 0
	c = std.Badchar
	while true
		c = takec(ts)
		if std.isxdigit(c)
			v *= 16
			v += std.charval(c, 16)
		else
			break
		;;
		if v > 0x10FFFF
			err(ts.loc, "invalid codepoint in \\u escape sequence\n")
		;;
	;;
	if c != '}'
		err(ts.loc, "\\u escape sequence without closing '{'\n")
	;;
	-> v
}

const numlit = {ts
	var t

	if matchc(ts, '0')
		if matchc(ts, 'x')
			t = number(ts, 16)
		elif matchc(ts, 'b')
			t = number(ts, 2)
		elif matchc(ts, 'o')
			t = number(ts, 8)
		else
			t = number(ts, 10)
		;;
	else
		t = number(ts, 10)
	;;
	-> t
}

const number = {ts, base
	var buf, nbuf
	var isfloat, issigned
	var v

	buf = ts.data[ts.idx:]
	nbuf = 0
	isfloat = false
	for var c = peekc(ts); std.isxdigit(c) || c == '.' || c == '_'; c = peekc(ts)
		takec(ts)
		if c == '_'
			continue
		elif c == '.'
			isfloat = true
		else 
			v = std.charval(c, base)
			if v < 0
				err(ts.loc, "digit {} out of range of base {}\n", c, base)
			;;
		;;
		nbuf++
	;;

	if isfloat
		if base != 10
			err(ts.loc, "floats must be in base 10\n")
		;;
		std.fatal("unable to parse floats: fuck me\n")
		/*
		-> `Tfltlit std.flt64parse(buf[:n])
		*/
	else
		issigned = true
		if peekc(ts) == 'u'
			takec(ts)
			issigned = false
		;;
		v = std.get(std.intparsebase(buf[:nbuf], base))
		-> `Tintlit v
	;;
}

const kwident = {ts
	match identstr(ts)
	| "do":	-> `Tdo
	| "break": 	-> `Tbreak
	| "const": 	-> `Tconst
	| "continue": 	-> `Tcontinue
	| "else": 	-> `Telse
	| "extern": 	-> `Textern
	| "for": 	-> `Tfor
	| "goto": 	-> `Tgoto
	| "if": 	-> `Tif
	| "sizeof": 	-> `Tsizeof
	| "struct": 	-> `Tstruct
	| "enum": 	-> `Tenum
	| "typedef":	-> `Ttypedef
	| "union": 	-> `Tunion
	| "void": 	-> `Tvoid
	| "while": 	-> `Twhile
	| "return": -> `Treturn
	| "char":	-> `Tchar
	| "int":	-> `Tint
	| "switch":	-> `Tswitch
	| "case":	-> `Tcase
	| "default":	-> `Tdefault
	| "long":	-> `Tlong
	| ident:	-> `Tident ident
	;;
}

const oper = {ts
	var t, chr

	chr = takec(ts)
	match chr
	| '{': t = `Tobrace
	| '}': t = `Tcbrace
	| '(': t = `Toparen
	| ')': t = `Tcparen
	| '[': t = `Tosqbrac
	| ']': t = `Tcsqbrac
	| ',': t = `Tcomma
	| '~': t = `Tbnot
	| ':': t = `Tcolon;
	| ';': t = `Tsemi
	| '.':
		if npeekc(ts, 1) == '.' && npeekc(ts, 2) == '.'
			takec(ts)
			takec(ts)
			t = `Tellipsis;
		else
			t = `Tdot;
		;;
	| '+':
		if matchc(ts, '=')
			t = `Taddeq
		elif matchc(ts, '+')
			t = `Tinc
		else
			t = `Tplus
		;;
	| '-':
		if matchc(ts, '=')
			t = `Tsubeq
		elif matchc(ts, '>')
			t = `Tarrow
		elif matchc(ts, '-')
			t = `Tdec
		else
			t = `Tminus
		;;
	| '*':
		if matchc(ts, '=')
			t = `Tmuleq
		else
			t = `Tmul
		;;
	| '/':
		if matchc(ts, '=')
			t = `Tdiveq
		else
			t = `Tdiv
		;;
	| '%':
		if matchc(ts, '=')
			t = `Tmodeq
		else
			t = `Tmod
		;;
	| '=':
		if matchc(ts, '=')
			t = `Teq
		else
			t = `Tasn
		;;
	| '|':
		if matchc(ts, '=')
			t = `Tboreq
		elif matchc(ts, '|')
			t = `Tlor
		else
			t = `Tbor
		;;
	| '&':
		if matchc(ts, '=')
			t = `Tbandeq
		elif matchc(ts, '&')
			t = `Tland
		else
			t = `Tband
		;;
	| '^':
		if matchc(ts, '=')
			t = `Tbxoreq
		else
			t = `Tbxor
		;;
	| '<':
		if matchc(ts, '=')
			t = `Tle;
		elif matchc(ts, '<')
			if matchc(ts, '=')
				t = `Tbsleq
			else
				t = `Tbsl
			;;
		else
			t = `Tlt;
		;;
	| '>':
		if matchc(ts, '=')
			t = `Tge
		elif matchc(ts, '>')
			if matchc(ts, '=')
				t = `Tbsreq
			else
				t = `Tbsr
			;;
		else
			t = `Tgt;
		;;

	| '!':
		if matchc(ts, '=')
			t = `Tne
		else
			t = `Tlnot
		;;
	| '#':
		t = `Thash
	| c:
		t = `Terror;
		err(ts.loc, "junk character {}", c);
	;;
	-> t
}

const identstr = {ts
	var str, i

	str = ts.data[ts.idx:]

	for i = 0; i < str.len; i++
		if !isident((str[i] : char))
			break
		;;
	;;
	str = str[:i]
	ts.idx += str.len
	-> str
}

const isident = {c
	-> c & 0x80 == 0 && \
		(c >= 'a' && c <= 'z' || \
		 c >= 'A' && c <= 'Z' || \
		 c >= '0' && c <= '9' || \
		 c == '_' || c == '$')
}

const peekc = {ts
	-> std.decode(ts.data[ts.idx:])
}

const npeekc = {ts, n
	var c, s

	s = ts.data[ts.idx:]
	for var i = 0; i < n; i++
		(c, s) = std.strstep(s)
	;;
	-> std.decode(s)
}

const takec = {ts
	var c

	(c, _) = std.strstep(ts.data[ts.idx:])
	match c
	| '\n':
		ts.loc.line += 1
		ts.loc.col = 1
	| '\t':
		ts.loc.col += 4
	| _:
		ts.loc.col += 1
	;;
	ts.idx += (std.charlen(c) : int)
	-> c
}

const skipto = {ts, chr
	var c

	while true
		c = peekc(ts)
		if ts.idx == ts.data.len || c == chr
			break
		;;
		takec(ts)
	;;
}

const matchc = {ts, chr
	var c

	(c, _) = std.strstep(ts.data[ts.idx:])
	if c == chr
		ts.idx += (std.charlen(c) : int)
		-> true
	else
		-> false
	;;
}

