use std
use "ctypes"

pkg qc = 
	
	type node = union
		`Nfunc func#
		`Ndecl (sym#, std.option(expr#))[:]
	;;
	
	type func = struct
		name : byte[:]
		functy : cfunc
		body : stmt#[:]
		locals : sym#[:]
		params : sym#[:]
		lblcnt : int
	;;
	
	type stmt = union
		`Sdecl (sym#, std.option(expr#))[:]
		`Sexpr expr#
		`Sreturn expr#
		`Sblock stmt#[:]
		`Sif ifstmt
		`Sfor forstmt
		`Swhile whilestmt
		`Sdowhile whilestmt
		`Slabeled labeledstmt
		`Sgoto int
		`Sempty
	;;
	
	type ifstmt = struct
		cond : expr#
		iftrue : stmt#
		iffalse : std.option(stmt#)
		truelbl : int
		falselbl : int
		donelbl : int
	;;
	
	type forstmt = struct
		init : std.option(expr#)
		cond : std.option(expr#)
		step : std.option(expr#)
		stmt : stmt#
		contlbl : int
		stmtlbl : int
		breaklbl : int
	;;
	
	type whilestmt = struct
		cond : expr#
		stmt : stmt#
		contlbl : int
		breaklbl : int
		stmtlbl : int
	;;

	type labeledstmt = struct
		stmt : stmt#
		lbl : int
	;;
	
	type expr = union
		`Ebinop binop
		`Elogop logop
		`Erelop relop
		`Eptradd ptradd
		`Eptrdiff ptrdiff
		`Eunop unop
		`Eassign assign
		`Eintlit int64
		`Estrlit byte[:]
		`Eident sym#
		`Eidx index
		`Esel selector
		`Ecall call
		`Ecast cast
		`Eincdec incdec
		`Esizeof int
	;;
	
	type cast = struct
		ty : ctype
		expr : expr#
	;;

	type selector = struct
		ty : ctype
		expr : expr#
		off : uint32
	;;

	type call = struct
		ty : ctype
		func : expr#
		args : expr#[:]
	;;
	
	type index = struct
		ty : ctype
		expr : expr#
		idx : expr#
	;;
	
	type aop = union
		`Oasn
		`Oaddasn
		`Osubasn
	;;
	
	type bop = union
		`Oplus
		`Ominus
		`Omul
		`Odiv
		`Omod
		`Oxor
		`Oor
		`Oand
	;;
	
	type uop = union
		`Olnot
		`Obnot
		`Oneg
		`Oaddr
		`Oderef
	;;
	
	type incop = union
		`Opreinc
		`Opredec
		`Opostinc
		`Opostdec
	;;
	
	type logbinop = union
		`Olor
		`Oland
	;;
	
	type relbinop = union
		`Oeq
		`One
		`Olt
		`Ogt
		`Oge
		`Ole
	;;
	
	type relop = struct
		op : relbinop
		l : expr#
		r : expr#
	;;
	
	type logop = struct
		op : logbinop
		l : expr#
		r : expr#
	;;
	
	type incdec = struct
		ty : ctype
		op : incop
		expr : expr#
	;;
	
	type binop = struct
		ty : ctype
		op : bop
		l : expr#
		r : expr#
	;;
	
	type ptradd = struct
		ptr : expr#
		amnt : expr#
	;;
	
	type ptrdiff = struct
		l : expr#
		r : expr#
	;;
	
	type unop = struct
		ty : ctype
		op : uop
		operand : expr#
	;;
	
	type assign = struct
		ty : ctype
		op : aop
		l : expr#
		r : expr#
	;;
	
	type sclass = union
		`Sclasslocal
		`Sclassglobal
		`Sclasstypedef
		`Sclassnone
	;;
	
	type sym = struct
		name : byte[:]
		ty : ctype
		sc : sclass
	;;
	
	const nextlbl : (f : func# -> int)
	const exprtype : (e : expr# -> ctype)
	const islval : (e : expr# -> bool)
;;

// XXX this label count is global until qbe supports func local labels.
var lblcnt = 0

const nextlbl = {f
	-> lblcnt++
	//-> f.lblcnt++
}

const exprtype = {e
	match e
	| &(`Ecast cast):
		-> cast.ty
	| &(`Esel sel):
		-> sel.ty
	| &(`Eident sym):
		-> sym.ty
	| &(`Ebinop bop):
		-> bop.ty
	| &(`Eunop uop):
		-> uop.ty
	| &(`Eptrdiff _):
		/* XXX correct type? */
		-> `Tylong
	| &(`Eptradd add):
		-> exprtype(add.ptr)
	| &(`Eassign asn):
		-> asn.ty
	| &(`Eintlit _):
		-> `Tyint
	| &(`Elogop _):
		-> `Tyint
	| &(`Erelop _):
		-> `Tyint
	| &(`Estrlit _):
		-> `Tyaggr std.mk(`Typtr `Tychar)
	| &(`Eidx idx):
		-> idx.ty
	| &(`Ecall call):
		-> `Tyint
	| &(`Eincdec incdec):
		-> exprtype(incdec.expr)
	| &(`Esizeof _):
		-> `Tyint
	;;
}

const islval = {n
	match n#
	| `Eunop unop:
		match unop.op
		| `Oderef:
			-> true
		| _:
			-> false
		;;
	| `Eident _:
		-> true
	| `Eidx _:
		-> true
	| `Esel _:
		-> true
	|_:
		-> false
	;;
}
